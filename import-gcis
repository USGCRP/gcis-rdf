#!/usr/bin/env perl

use Mojo::UserAgent;
use File::Temp;
use Smart::Comments;
use Time::Duration qw/duration/;
use v5.14;

use lib '.';
use Virtuoso;

my $url_base  = q[http://data.gcis-dev-front.joss.ucar.edu];
my $report_id = q[nca3draft];

my $ua = Mojo::UserAgent->new();
my $v = Virtuoso->new(
     user => "__dbinfohere__",
     pass => "__dbinfohere__",
     graph_uri => q[http://tmp.data.globalchange.gov]
);

&main;

sub info($) {
    my $msg = shift;
    say sprintf('%30s: %s',scalar localtime, $msg);
    $v->logger->info($msg);
}

sub debug {
    my $msg = shift;
    $v->logger->info($msg);
}

sub get_to_tmp {
    my $path = shift or die "no path";
    my $query = shift || {};
    #info $path;
    my $url  = Mojo::URL->new($url_base)->path($path.'.ttl');
    $url->query(%$query);
    my $tx   = $ua->get($url);
    my $res  = $tx->success or die "Error getting $url : " . $tx->error;
    my $tmp  = File::Temp->new;
    $res->content->asset->move_to("$tmp") or die $!;
    return $tmp;
}

sub get_json {
    my $path = shift;
    my $query = shift || {};
    my $url  = Mojo::URL->new($url_base)->path($path.'.json');
    $url->query(%$query);
    my $tx   = $ua->get($url);
    my $res  = $tx->success or die "Error getting $url : " . $tx->error;
    my $json = $res->json or die $url.' : '.$res->message.' : '.$res->body;
    return $json;
}

sub main {
    my $start = time;
    $v->drop_graph;
    $v->drop_graph('http://tmp2.data.globalchange.gov');
    for my $resource (qw/article journal person organization/) {
        info $resource;
        my $instances = get_json("/$resource", { all => 1 });
        for my $instance (@$instances) { ### importing-->[%]     done
            debug $instance->{uri};
            $v->load_ttl_file(get_to_tmp("$instance->{uri}"));
        }
    }

    my $json = get_json("/report", { all => 1 });
    for my $report (@$json) {
        $v->load_ttl_file( get_to_tmp("/report/$report->{identifier}") );
        my $id = $report->{identifier};
        info "report $id";
        for my $resource (qw/figure finding chapter reference image/) {
            my $items = get_json("/report/$id/$resource", { all => 1 });
            info $resource if @$items;
            for my $item (@$items) { ### importing--->[%]      done
                debug $item->{uri};
                $v->load_ttl_file(get_to_tmp("$item->{uri}"));
            }
        }
    }
    info "Renaming";
    $v->drop_graph('http://data.globalchange.gov');
    $v->rename_graph_to('http://data.globalchange.gov');
    say "time elapsed : ".duration(time - $start);
}

